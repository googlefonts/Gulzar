Include "shared.fez";
LoadPlugin qalamTools.SeparateConsecutive;

# These are a more ad-hoc collection of positioning rules.
# We put them in mkmk because we want them to be processed
# at the end, after anchor positioning is done.

DefineClass @narrowmedi = /.*m\d+$/  & (run < 350);

# Routines to move final jim dots around, to help fit sequences like
# ہپچ
Routine FinaJimTopRight {
  Position ([sdb.two ddb.two tdb.two] <yPlacement=-700>);
  Position ([sdb.one ddb.one tdb.one] <yPlacement=-700>);
  Position ([haydb.one haydb.two] <yPlacement=-300>);
  Position ([sdb ddb tdb] <xPlacement=+80 yPlacement=+80>);
  Position ([haydb] <xPlacement=+80 yPlacement=+80>);
};

Routine FinaJimDownABit {
  Position (/sdi|tdi/ <yPlacement=-80 xPlacement=-50>);
};

Routine FinaQafDownRight {
  Position ([tdb] <yPlacement=-280 xPlacement=40>);
  Position ([sdb ddb] <yPlacement=-280 xPlacement=40>);
};

Routine FinaQafUpLeft {
  Position ([tdb] <xPlacement=-120 yPlacement=+80>);
};

Feature kern {
  # Sequences like پی have problems - where is the right place to
  # put the dots? We tuck them down out of the way.
  Routine GiveChYeDotsMoreSpace {
    Position [sdb ddb tdb] ALIFf1 [BEi11 TEi11 LAMi11 LAMi7 LAMi2 LAMi9] (sdb.yb <yPlacement=-150>) [CH_YEf1 CH_YEf2 NUNf1]; # یابی


    Position (@bigbowlfina @inits <xAdvance=+100> [ddb.yb tdb.yb] <200 -180 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ([ddb.yb tdb.yb] <200 -180 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position [REf2 REf3] @inits ( sdb.yb <180 -180 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position [sdb.one tdb.one ddb.one] @DOTm ( [ddb.yb sdb.yb] <0 -250 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( sdb.yb <120 -50  0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position [BEi7 TEi7] ( /.yb|tdb|ddb|sdb/ <0 0 0 0> ) /TEm|BEm|JIMm/ [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /.yb|tdb|ddb|sdb/ <150 0 0 0> ) /LAMm|JIMm13/ [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /.yb|tdb|ddb|sdb/ <0 0 0 0> ) BEm13 [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /.yb|tdb|ddb|sdb/ <120 0 0 0> ) @narrowmedi [CH_YEf1 CH_YEf2 NUNf1]; # علیمی

    Position ( /HAYCi/ <xAdvance=+50> /haydb/ <150 -50  0 0> ) [CH_YEf1 CH_YEf2 NUNf1]; # سیاہی
    Position ( /haydb/ <150 -50  0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /HAYCi/ <xAdvance=+50> /haydb/ <80 -120 0 0> ) @narrowmedi [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /haydb/ <80 -120 0 0> ) @narrowmedi [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /HAYCi/ <xAdvance=+50> /haydb/ <xPlacement=50 yPlacement=-25> ) @medis JIMf1;
    Position ( /haydb/ <xPlacement=50 yPlacement=-25> ) @medis JIMf1;
    Position ( /HAYCi/ <xAdvance=+50> /haydb/ <xPlacement=50 yPlacement=-25> ) JIMf1;
    Position ( /haydb/ <xPlacement=50 yPlacement=-25> ) JIMf1;

  } UseMarkFilteringSet /haydb|sdb|tdb|ddb|haydb/ IgnoreLigatures;

  DefineClass @WideFinals = @finas & (xMin < 50);

  DefineClass @NotTooHighStuff = (@finas|@isols) & (yMax < 600);
  DefineClass @NarrowMedials = /[mf](sd)?\d+$/ & (run < 400);
  DefineClass @NarrowInits = /i(sd)?\d+$/ & (run < 400);

  Routine FinaJimFixes {
    Chain /(JIM|[BT]E)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight @NarrowMedials  JIMf1 /tdi|sdi/ ^FinaJimDownABit);
    Chain /(JIM|[BT]E)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight @NarrowMedials /sdi|tdi|ddb|sdb|tdb|haydb/ ^FinaJimTopRight JIMf1 /tdi|sdi/ ^FinaJimDownABit);
    Chain /(JIM|[BT]E)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight JIMf1 /tdi|sdi/ ^FinaJimDownABit);

    #Chain /(HAYC)[mi]/ (/haydb/ ^FinaJimTopRight @NarrowMedials JIMf1 /tdi|sdi/ ^FinaJimDownABit);
    Chain /(HAYC)[mi]/ (/haydb/ ^FinaJimTopRight @NarrowMedials /sdi|tdi|ddb|sdb|tdb|haydb/ ^FinaJimTopRight  JIMf1 /tdi|sdi/ ^FinaJimDownABit);
    Chain /(HAYC)[mi]/ (/haydb/ ^FinaJimTopRight JIMf1 /tdi|sdi/ ^FinaJimDownABit);

    # Put AIN/QAF here too since it's basically the same
    #Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight @NarrowMedials AINf1);
    #Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight @NarrowMedials /sdi|tdi|ddb|sdb|tdb|haydb/ ^FinaJimTopRight AINf1);
    #Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight AINf1);
    Chain /(JIM|[BT]E)[mi]/ (/sdb|ddb|tdb/ ^FinaQafDownRight QAFf1);
  } UseMarkFilteringSet /sdi|tdi|ddb|sdb|tdb|haydb/;

  Routine Various2 {
    Position [@inits @narrowmedi] @below_dots @BEm ([sdb.one ddb.one] <yPlacement=+100 xPlacement=-75>) AINf1;
    Position ([sdb.one ddb.one tdb.one] <yPlacement=+200 xPlacement=-75>) AINf1;
    Position (haydb <yPlacement=+40 xPlacement=-105>) AINf1;
    Position [sdb ddb tdb] @medis (sdb.yb <xPlacement=50>) AINf1; # مبلغ

    # GrossKasraAvoidanceThing
    Position [sdb.yb ddb.yb tdb.yb] ( KASRA.yb <yPlacement=-200> ) AINf1;
    Position ( KASRA.yb <yPlacement=+200> ) AINf1;
    Position [sdb.yb ddb.yb tdb.yb] ( KASRA.yb <yPlacement=-400> ) JIMf1;

    # Add space around pointy damma
    Position @NotTooHighStuff DAMMA (space <xAdvance=+50> );

    # Finess Init kasra yeh barree e.g. رِیے
    Position KASRA ( @inits <xAdvance=+200> ) /.yb/;

    # Clear Alif Madda
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+200> ) HAMZA_ABOVE;
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+120> ) [dda tda toeda];
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+150> ) [@all_above_marks @below_dots] /[KG]AFm/;
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+75> ) @all_above_marks;
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+75> ) @below_dots @all_above_marks;
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+0> ) endOfBariYe;
    # Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+0> ) [@dal_like @vao_like /SINi/];
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+150> ) /[KG]AFm/;
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+75> );
    Position [@alif_like @vao_like] HAMZA_ABOVE ( /[ui](sd)?\d/ <xAdvance=+50> );

    # Heh Alif
    Position HAYCi3 (haydb <yPlacement=+180>) ALIFf1;

    # Lam Khah
    Position /LAM[im]/ /JIM[mf]/ ([sda sda.one] <xPlacement=-50>);

    # Just fiddling now
    Position  [parenright.latin parenright.urdu] (/[BT]Ei/ <xAdvance=+120>) [dda ddb tdb];

    Position DAMMA ([@dal_like @vao_like @inits] <xAdvance=+100> [dda tda HAMZA_ABOVE]);

    Position dda ([VAOf1 REf1 DALf1] /TEi/ <xAdvance=+50> dda <xPlacement=-50>);

    # چنا
    Position TEi3 ([dda tda] <xPlacement=+50>) ALIFf1;

    Position (BEi13 <xAdvance=+70>) [ddb.yb tdb.yb];

    Position TEisd1 [tdb ddb] TEm3 (ddb.one <yPlacement=0>) [@narrowmedi /[KG]AFm7/] [AINf1 JIMf1 CH_YEf1]; # پیکج 
    Position TEisd1 [tdb ddb] TEm3 (ddb.one <yPlacement=-200>); # پیدا

  };
  Routine ShortBes {
    # Short BE/TEs
    Position @alif_like ([TEi3 BEi3] <xAdvance=+110>) [dda tda];
    Position @alif_like ([TEi16 BEi16] <xAdvance=+90>) [dda tda];
    Position @alif_like ([TEi15 BEi15] <xAdvance=+20>) [dda tda];
    Position @alif_like HAMZA_ABOVE ([TEisd1 BEisd1] <xAdvance=+25>);
    Position [ALIFf1 ALIFu12] HAMZA_ABOVE (/SADi/ <xAdvance=+90>);
    Position @alif_like HAMZA_ABOVE ([TEi3] <xAdvance=+80>) [dda tda];
    Position @vao_like ([TEi3 BEi3 TEi16 BEi16] <xAdvance=+60>) [dda tda];

    # Clear Alif Nun KL
    Position /ALIF[uf]1/ ([BEi8 TEi8] <xAdvance=+100>) /sda|dda/;
  } IgnoreLigatures UseMarkFilteringSet [sda dda tda HAMZA_ABOVE];

  Routine ShaddaClashes {
    Position HAYCi7 JIMm11 (SHADDA <yPlacement=+50>);
    Position /JIMm|BEi9/ @medis (SHADDA <yPlacement=+150 xPlacement=-50>);
  } UseMarkFilteringSet [SHADDA];

  Routine ClashesToRight {
    Position LAMi4 /FEm/ (dda <xPlacement=0>) /ALIFf|LAM[mf]/;
    Position LAMi4 /FEm/ (dda <xPlacement=-30>);
    Position /JIM[mi]/ TEm11 (HAMZA_ABOVE <xPlacement=-50>);
    Position /JIM[mi]/ [sda dda] TEm11 (HAMZA_ABOVE <xPlacement=-50>);
    Position /[KG]AF/ [BEm3 TEm3] (tda <yPlacement=-150>) /[KG]AF/;
    Position /[KG]AF/ [BEm3 TEm3] ([tda] <yPlacement=-150> @all_above_marks <yPlacement=+350>) /[KG]AF/;
    Position /[KG]AF/ [BEm3 TEm3] ([sda dda] <yPlacement=-50> @all_above_marks <yPlacement=+350>) /[KG]AF/;
    Position /KAFi/ TEm3 (tda <yPlacement=+250>) @alif_like;
    Position KAFi2 /TEm/ (tda <xPlacement=-50>);
    Position /FEi/ ([sda dda] <xPlacement=+50 yPlacement=+90> /[TB]Em/ [dda tda] <yPlacement=+120 xPlacement=+25>) [@alif_like LAMf1];
    Position /FEi/ [sda dda] /[TB]Em/ ([dda tda HAMZA_ABOVE] <xPlacement=-120>);
    Position /FEi/ [sda dda] /[TB]Em/ (sda <xPlacement=-70>);
    Position FEi7 [sda dda] /JIMm/ ([sda dda tda] <xPlacement=-100>);
    Position FEi7 /JIMm/ ([sda dda tda] <xPlacement=-100>);

    Position FEi15 [sda dda] QAFf1 ([sda dda tda] <xPlacement=-100>);

    Position FEi16 REf1 ([sda toeda] <xPlacement=-75>);
    Position FEi16 [sda dda] REf1 ([sda toeda] <xPlacement=-75>);

    Position TEm3 (tda <yPlacement=-120>) @alif_like MADDA;
    Position TEm7 ([tdb ddb] <yPlacement=+20>) JIMm13;
    Position /FEi/ [sda dda] [@finas @medis] ([dda HAMZA_ABOVE] <xPlacement=-50>);
    Position /FEi/ [@finas @medis] ([dda HAMZA_ABOVE] <xPlacement=-50>);

    # حنفی
    Position BEm4 (sda <yPlacement=+100>) /FEm/ sda;

    Position /JIMi/ [sda dda tda] REf1 ([sda toeda] <xPlacement=-120>);
    Position /JIMi/ REf1 ([sda toeda] <xPlacement=-120>);

    Position /JIMi/ [sda dda tda] /[TB]Em|HAYCf/ ([sda dda] <xPlacement=-20 yPlacement=+20>) @alif_like;
    Position /JIMi/ /[TB]Em|HAYCf/ ([sda dda] <xPlacement=-20 yPlacement=+20>) @alif_like;
    Position /JIMi/ [sda dda tda] /[TB]Em|HAYCf/ ([sda dda] <xPlacement=-80 yPlacement=+0>);
    Position /JIMi/ /[TB]Em|HAYCf/ ([sda dda tda] <xPlacement=-80>);

    Position /JIMi/ [sda dda tda] /JIMm/ (sda <xPlacement=-20 yPlacement=+30>);
    Position /JIMi/ /JIMm/ (sda <xPlacement=-20 yPlacement=+30>);

    Position /AIN[mi]/ /JIMm/ ([sda] <yPlacement=+175 xPlacement=-30>);
    Position /AIN[mi]/ sda /JIMm/ ([sda] <yPlacement=+175 xPlacement=-90>);

    Position /LAM[mi]/ TEm3 ([dda tda] <xPlacement=-20>) [@alif_like /LAM[mf]/];  # لثا 

    # فت
    Position @alif_like ([BEisd1 TEisd1 BEi3 TEi3] <xAdvance=+50>) HAMZA_ABOVE;

    Position @long_re [sda] (/[BT]Ei|JIMi/ <xAdvance=+50>) [sda tda dda] ;
    Position /JIM[mi]/ REf1 ([sda toeda] <yPlacement=+75>);
    # Position /AIN[mi]/ @long_re ([sda toeda] <yPlacement=+75 xPlacement=-30>);
    # Position /AIN[mi]/ sda @long_re ([sda toeda] <yPlacement=+75 xPlacement=-30>);
    Position /MIMi/ /JIMm/ (sda <xPlacement=-70 yPlacement=+70>);
    Position /AINm/ /SINm|[BT]Em/ ([sda dda tda] <yPlacement=+0 xPlacement=-0>) @alif_like;
    Position /AINm/ /SINm|[BT]Em/ ([sda dda tda] <yPlacement=+50 xPlacement=-30>);
    Position [@vao_like @dal_like] ALIFu1 (HAMZA_BELOW <yPlacement=-100>);

    # طنز
    Position BEm16 @long_re ([sda toeda] <xPlacement=-250>);
    Position BEm16 [sda] @long_re ([sda toeda] <xPlacement=-200>);
    Position TEm16 [dda tda] @long_re ([sda toeda] <xPlacement=-200>);

    # ربطة
    Position /(TOE|LAM)[mi]/ HAYCf1 (dda <xPlacement=-50>);

    # زش
    Position [REu1 REf1] ([sda toeda] <xPlacement=+75>) /SIN[ui]/ [sda dda tda];
    Position [REu1 REf1] [sda toeda] (/TE[ui]/ <xAdvance=+40>) [sda dda tda] @finas;

    # لتركية
    Position [tda dda] @long_re (/[KG]AFi/ <xAdvance=+80>);

  } UseMarkFilteringSet [ sda dda tda toeda HAMZA_ABOVE MADDA HAMZA_BELOW];

  Routine ClashesBelow {
    # Final ChYe avoidance
    Position ([ddb tdb] <xPlacement=+50>) /JIMm/ [ddb tdb] CH_YEf1;
    Position ([ddb tdb] <xPlacement=+50>) /JIMm/ CH_YEf1;

    Position /TEm5/ ([ddb tdb] <yPlacement=-50>) /HAYAm/;

  } UseMarkFilteringSet [ddb tdb];

  Routine DotsUnderGaf {
    Position (/\.two$/ <yPlacement=-220 xPlacement=+40>) @NarrowMedials @all_above_marks /KAF|GAF/;
    Position /\.two$/ @NarrowMedials (@all_above_marks <yPlacement=-50>) /KAF|GAF/;
    Position (/\.two$/ <yPlacement=-220 xPlacement=+40>) @all_above_marks /KAF|GAF/;

    Position ([sda dda] <yPlacement=0 xPlacement=+65>) @NarrowMedials /\.one$/ @NarrowMedials @all_above_marks /KAF|GAF/;
    Position (/\.one$/ <yPlacement=-120 xPlacement=+45>) @NarrowMedials @all_above_marks /KAF|GAF/;
    Position /\.one$/ @NarrowMedials (@all_above_marks <yPlacement=-40>) /KAF|GAF/;
    Position (/\.one$/ <yPlacement=-120 xPlacement=+45>) @all_above_marks /KAF|GAF/;
    Position (/\.one$/ <yPlacement=-20>) /KAF|GAF/;

    Position (toeda <yPlacement=-60>) /[KG]AF[mf]/;
    Position @alif_like (/FE[mi]/ <xAdvance=+70> [sda dda tda] <yPlacement=-80 xPlacement=+80>) /[KG]AFf/;
    Position /FEi8/ ([sda dda tda] <yPlacement=0 xPlacement=0>) /[KG]AF[mf]/;
    Position /FE[mi]/ ([sda dda tda] <yPlacement=-80 xPlacement=+80>) /[KG]AF[mf]/;

    Position [sda dda tda toeda] (DAMMA <yPlacement=+120>) /[KG]AF[mf]/;
    
    Position FEi2 ([sda dda tda] <yPlacement=-50 xPlacement=+70>) @BEm [sda dda tda] /[KG]AF[mf]/;

    Position TEm7 ([sda dda tda] <yPlacement=-100>) /JIMm/ /[KG]AF[mf]/; # مستحکم
    # Let's clear it
    Position /FE[im]/ (/sda|dda/ <yPlacement=+380>) /(KAF|GAF)m1[36]/;
    Position /FEi/ ([sda dda tda] <yPlacement=-70 xPlacement=+90>) [@BEm /FEm/]  /[KG]AF[mf]/;
    Position ([sda dda tda] <yPlacement=-60  xPlacement=+90>) /FEm/ [sda dda tda] /[KG]AF[mf]/;
    Position MADDA (/KAF|GAF/ <xAdvance=+0>) BARI_YEf1;
    Position MADDA (/KAF|GAF/ <xAdvance=+0>) @medis @medis;
    Position MADDA (/KAF|GAF/ <xAdvance=+200>);

    Position (toeda <yPlacement=-50>) @finas /[KG]AF[iu]/;

    # Deeply unfortunate sequence
    Position SINm9 (tda <yPlacement=-150 xPlacement=0>) MIMm3 /[KG]AF[mf]/;
    Position /[KG]AFi/ (DAMMA <yPlacement=+200>) @narrowmedi /[KG]AF/;

    Position (HAMZA_ABOVE <yPlacement=+200>) /[KG]AFi2/;
    } UseMarkFilteringSet /toeda|tda|DAMMA|sda|dda|MADDA|HAMZA_ABOVE/;

  DefineClass @TallAndStraight = /LAM[im]|[KG]AF[im]|JIM[im]/;
  DefineClass @LowNotTeh = /^(?!TE|LAM|[KG]AF|ALIF)\w+[mf]\d+/;
  DefineClass @NarrowTeh = /^TE[mf](sd)?\d+/ & (run < 300);
  DefineClass @TallFinal = /LAM[mf]|[KG]AF[mf]|JIM[mf]|ALIFf1/;
  Routine HighTehFinesse {
    Position @TallAndStraight /TEm|JIMm/ ( [sda tda dda] <xPlacement=-150> ) /_YEf/;
  }  UseMarkFilteringSet [sda dda tda];

  Routine TdbAinFinesse {
    Position (tdb.yb <xPlacement=-30>) AINf1;
  } UseMarkFilteringSet [tdb.yb];


  Routine AddIsolDammaSpace {
    Position [@isols @finas] DAMMA ( @inits <xAdvance=+100> );
  } UseMarkFilteringSet [DAMMA];

  Routine MoreKerningFixes {
    Position REf2 (TEi12 <xAdvance=+100>); # Particular nasty case: 
    Position /ALIF/ (MIMi7 <xAdvance=+150>);
  } IgnoreLigatures UseMarkFilteringSet /\.yb/;

  Routine Various3 {
    # InitChechFinesse
    Position ( /JIMi/ <xAdvance=+50> tdb <xPlacement=+50> );
    # AvoidZahIsol
    Position [REf2 REf3] [sda tda DAMMA] (@isols <xAdvance=+120>);
    Position [REf2 REf3] toeda (@isols <xAdvance=+160>);
    Position [REf1] [sda tda toeda] (/SAD[ui]/ <xAdvance=+250>); # فزض
  };
};
