Include "shared.fez";
LoadPlugin qalamTools.SeparateConsecutive;

# And now we do some more positioning. We put this in mkmk because we want
# this to be processed at the end, after anchor positioning is done.

DefineClass @narrowmedi = /.*m\d+$/  & (run < 350);

Routine FinaJimTopRight {
  Position ([sdb.two ddb.two tdb.two] <yPlacement=-700>);
  Position ([sdb.one ddb.one tdb.one] <yPlacement=-600>);
  Position ([haydb.one haydb.two] <yPlacement=-300>);
  Position ([sdb ddb tdb] <xPlacement=+80 yPlacement=+80>);
  Position ([haydb] <xPlacement=+80 yPlacement=+80>);
};

Routine FinaJimDownABit {
  Position (/sdi|tdi/ <yPlacement=-80>);
};

Routine FinaQafDownRight {
  Position ([tdb] <yPlacement=-280 xPlacement=40>);
  Position ([sdb ddb] <yPlacement=-280 xPlacement=40>);
};

Routine FinaQafUpLeft {
  Position ([tdb] <xPlacement=-120 yPlacement=+80>);
};

Feature kern {
  Routine GiveChYeDotsMoreSpace {
    Position [sdb ddb tdb] ALIFf1 [BEi11 TEi11 LAMi11 LAMi7 LAMi2 LAMi9] (sdb.yb <yPlacement=-150>) [CH_YEf1 CH_YEf2 NUNf1]; # یابی

    Position ( [ddb.yb tdb.yb] <0 -180 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position [REf2 REf3] @inits ( sdb.yb <80 -120 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( sdb.yb <80 -50  0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /haydb/ <150 -50  0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /.yb|tdb|ddb$/ <20 0 0 0> ) @narrowmedi [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /haydb/ <80 -120 0 0> ) @narrowmedi [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /haydb/ <xPlacement=50 yPlacement=-25> ) @medis JIMf1;
    Position ( /haydb/ <xPlacement=50 yPlacement=-25> ) JIMf1;
  } UseMarkFilteringSet /haydb|sdb.yb|tdb|ddb|haydb/;

  DefineClass @WideFinals = @finas & (xMin < 50);

  Routine Various1 {
    # Finesse Beh Haya
    Position /BE(sd)?i/ ([ddb tdb] <xPlacement=50>) /HAYAm/;

    # Finesse Beh JIM
    Position ( /ddb|tdb/ <xPlacement=+20> ) /JIMm/;
  } UseMarkFilteringSet /tdb|ddb$/;

  Routine KernBeHaya {
    Position (space <xAdvance=-100> ) /[BT]Ei/ /HAYA/;
  } IgnoreMarks;

  DefineClass @NotTooHighStuff = (@finas|@isols) & (yMax < 600);
  DefineClass @NarrowMedials = /[mf](sd)?\d+$/ & (run < 400);
  DefineClass @NarrowInits = /i(sd)?\d+$/ & (run < 400);

  Routine FinaJimFixes {
    Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight @NarrowMedials  JIMf1 /tdi|sdi/ ^FinaJimDownABit);
    Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight @NarrowMedials /sdi|tdi|ddb|sdb|tdb|haydb/ ^FinaJimTopRight JIMf1 /tdi|sdi/ ^FinaJimDownABit);
    Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight JIMf1 /tdi|sdi/ ^FinaJimDownABit);

    Chain /(HAYC)[mi]/ (/haydb/ ^FinaJimTopRight @NarrowMedials JIMf1 /tdi|sdi/ ^FinaJimDownABit);
    Chain /(HAYC)[mi]/ (/haydb/ ^FinaJimTopRight @NarrowMedials /sdi|tdi|ddb|sdb|tdb|haydb/ ^FinaJimTopRight  JIMf1 /tdi|sdi/ ^FinaJimDownABit);
    Chain /(HAYC)[mi]/ (/haydb/ ^FinaJimTopRight JIMf1 /tdi|sdi/ ^FinaJimDownABit);

    # Put AIN/QAF here too since it's basically the same
    #Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight @NarrowMedials AINf1);
    #Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight @NarrowMedials /sdi|tdi|ddb|sdb|tdb|haydb/ ^FinaJimTopRight AINf1);
    #Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight AINf1);
    
    Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaQafDownRight QAFf1);

    # Chain /(JIM|BE)[mi]/ (/tdb/ ^FinaQafUpLeft AINf1);


  } UseMarkFilteringSet /sdi|tdi|ddb|sdb|tdb|haydb/;

  Routine Various2 {
    # GrossKasraAvoidanceThing
    Position [sdb.yb ddb.yb tdb.yb] ( KASRA.yb <yPlacement=-200> ) AINf1;
    Position ( KASRA.yb <yPlacement=+200> ) AINf1;
    Position [sdb.yb ddb.yb tdb.yb] ( KASRA.yb <yPlacement=-400> ) JIMf1;

    # Add space around pointy damma
    Position @NotTooHighStuff DAMMA (space <xAdvance=+50> );

    # Finess Init kasra yeh barree e.g. رِیے
    Position KASRA ( @inits <xAdvance=+200> ) /.yb/;

    # Clear Alif Madda
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+150> ) HAMZA_ABOVE;
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+150> ) [dda tda];
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+100> ) @all_above_marks;
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+100> ) @below_dots @all_above_marks;
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+50> );

    # Heh Alif
    Position HAYCi3 (haydb <yPlacement=+159>) ALIFf1;
    # Clear Alif Nun KL
    Position /ALIF[uf]1/ (BEi8 <xAdvance=+50>) /sda|dda/ KAFm3;

    # Lam Khah
    Position /LAM[im]/ /JIM[mf]/ ([sda sda.one] <xPlacement=-50>);

    # Just fiddling now
    Position  [parenright.latin parenright.urdu] (/[BT]Ei/ <xAdvance=+70>) [dda tdb];

    # چنا 
    Position JIMi1 @nuktas /BEm3|TEm3/ ([sda dda tda] <yPlacement=+100>);
    Position JIMi1 /BEm3|TEm3/ ([sda dda tda] <yPlacement=+100>);

    # عزت 
    Position REf2 (sda <yPlacement=+200>) /[BT]E[iu]/;

    # ہا
    Position (haydb <yPlacement=+50>) ALIFf1;

    #  پڑتا
    #Position REf1 toeda ([@inits @isols] <xAdvance=+200>);
    #Position REf1 sda ([@inits @isols] <xAdvance=+75>);

    Position [@dal_like @vao_like] [toeda sda dda tda HAMZA_ABOVE] ([@dal_like @vao_like @BEi] <xAdvance=+100> [sda dda tda HAMZA_ABOVE toeda]);
    Position DAMMA ([@dal_like @vao_like @inits] <xAdvance=+100> [dda tda HAMZA_ABOVE]);

    Position @alif_like (/TEi/ <xAdvance=+75>);

  };

  Routine ClashesToRight {
    Position LAMi4 FEm11 (dda <xPlacement=-30>);
    Position HAYCi7 JIMm11 (SHADDA <yPlacement=+50>);
    Position FEm2 TEm11 (dda <xPlacement=-50>);
    Position FEm2 dda TEm11 (dda <xPlacement=-50>);
    Position /(KAF|GAF|LAM)i/ /TEm/ (dda <xPlacement=-50>);
  } UseMarkFilteringSet [ dda SHADDA];

  Routine DotsUnderGaf {
    Position (/\.one$/ <yPlacement=-220>) @NarrowMedials @all_above_marks /KAF|GAF/;
    Position /\.one$/ @NarrowMedials (@all_above_marks <yPlacement=-40>) /KAF|GAF/;
    Position (/\.one$/ <yPlacement=-220>) @all_above_marks /KAF|GAF/;

    Position [sda dda tda toeda] (DAMMA <yPlacement=+120>) /[KG]AF[mf]/;
    Position ([sda dda tda] <yPlacement=-50>) @BEm [sda dda tda] /[KG]AF[mf]/;
    Position ([sda dda tda] <yPlacement=-50>) @BEm  /[KG]AF[mf]/;
    Position TEm7 ([sda dda tda] <yPlacement=-70>) /JIMm/ /[KG]AF[mf]/; # مستحکم
    # Let's clear it
    Position /FE[im]/ (/sda|dda/ <yPlacement=+380>) /(KAF|GAF)m1[36]/;
    Position BEi8 (/DAMMA/ <yPlacement=+50>) /(KAF|GAF)m3/;
    Position MADDA (/KAF|GAF/ <xAdvance=+70>);
    } UseMarkFilteringSet /toeda|tda|DAMMA|sda|dda|MADDA/;

  DefineClass @TallAndStraight = /LAM[im]|[KG]AF[im]|JIM[im]/;
  DefineClass @LowNotTeh = /^(?!TE|LAM|[KG]AF|ALIF)\w+[mf]\d+/;
  DefineClass @NarrowTeh = /^TE[mf](sd)?\d+/ & (run < 300);
  DefineClass @TallFinal = /LAM[mf]|[KG]AF[mf]|JIM[mf]|ALIFf1/;
  Routine HighTehFinesse {
    Position @TallAndStraight /TEm|JIMm/ ( [sda tda dda] <xPlacement=-150> ) /_YEf/;
  }  UseMarkFilteringSet [sda dda tda];

  Routine TdbAinFinesse {
    Position (tdb.yb <xPlacement=-30>) AINf1;
  } UseMarkFilteringSet [tdb.yb];

  Routine FeTeFinesse {
    Position /FEi/ /TEm/ ( dda <xPlacement=-50 yPlacement=+50> ) ;
    Position /FEi/ dda /TEm/ ( dda <xPlacement=-50  yPlacement=+50> ) ;
    # قوتو
    Position dda ([VAOf1 REf1 DALf1] /TEi/ <xAdvance=+50> dda <xPlacement=-50>);
  } UseMarkFilteringSet [dda];

  Routine AddIsolDammaSpace {
    Position [@isols @finas] DAMMA ( @inits <xAdvance=+100> );
  } UseMarkFilteringSet [DAMMA];

  Routine MoreKerningFixes {
    Position REf2 (TEi12 <xAdvance=+100>); # Particular nasty case: 
    Position /ALIF/ (MIMi7 <xAdvance=+150>);
  } IgnoreLigatures UseMarkFilteringSet /\.yb/;

  Routine Various3 {
    # InitChechFinesse
    Position ( /JIMi/ <xAdvance=+50> tdb <xPlacement=+50> );
    # AvoidZahIsol
    Position [REf2 REf3] [sda tda toeda] (@isols <xAdvance=+50>);
  };
};
