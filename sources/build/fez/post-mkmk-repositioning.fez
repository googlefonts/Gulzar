Include "shared.fez";

# And now we do some more positioning. We put this in mkmk because we want
# this to be processed at the end, after anchor positioning is done.
Feature mkmk {

  # Dots and marks on the letter preceding a swash kaf need to be dropped
  # down to avoid collision. Dots get moved right slightly.
  # XXX ideally tall kaf/gaf and short kaf/gaf should be treated differently.
  DefineClass @kafgaf = /^(KAF|GAF)/;
  DefineClass @shortkafgaf = @kafgaf & (yMax < 700);
  DefineClass @narrowmedi = /.*m\d+$/  & (run < 250);

  # The SeparateConsecutive plugin performs the "knock-on" separation of
  # dots according to the Qalmi patent. It looks for a sequence of consecutive
  # dots up to a given length (3, in this case), and creates rules which
  # *cumulatively* move them all horizontally: it moves the second dot across
  # 70 units, the third dot 140 units, and so on. The mark filtering sets
  # establish whether or not the dots really are cumulative. Two below
  # dots are not cumulative if there is an above dot in the middle, and vice
  # versa.
  LoadPlugin qalamTools.SeparateConsecutive;
  DefineClass @behs = /.*[mif](sd)?\d+$/  & (run < 600); # any rasm can carry an accent
  Routine SeparateConsecutiveAboveDots {
    SeparateConsecutive @all_above_marks 4 60 -350;
  } UseMarkFilteringSet @all_above_marks;

  Routine SeparateConsecutiveBelowDots {
    SeparateConsecutive [sdb ddb tdb tdi KASRA ] 4 30 400;
  } UseMarkFilteringSet [sdb ddb tdb tdi KASRA];

  # Dots in a bari ye sequence, created by the BYMoveDots rule above, should
  # also be separated.
  DefineClass @behs = /.*[mif](sd)?\d+$/;

  Routine SeparateConsecutiveYBDots {
    # These numbers need to be small enough that reasonable cases work fine,
    # (test case: پپپے) but also big enough to cope with the "worst case" of
    # YB dots, which is probably stuff like پچے/چچے. Other nasty test cases
    # are مبچے and, for extra bonus fun points, ہچے and  پچہلے.
    # پپپے پچے چچے مبچے ہچے پچہلے
    SeparateConsecutive /.yb$/ 4 80 350;
  } UseMarkFilteringSet /.yb$/;

  # If the dots "poke out" the end of a narrow init glyph, we need to
  # add move space both to the dots and to the glyph itself so they
  # don't collide with preceding glyphs.
  DefineClass @overlapping_inits = @inits & (width < 350); # xMax[tdb]
  Routine OpenSpaceAroundSmallBehs {
    Position ( @overlapping_inits <xAdvance=0> @nuktas_and_kasra <xPlacement=0> )  BARI_YEf1; # Except here
    Position ( @overlapping_inits <xAdvance=200> @nuktas_and_kasra <xPlacement=50> )  /.*[mif]\d+$/;
  } UseMarkFilteringSet @nuktas_and_kasra;


#  Routine DropDotsOnTehGaf {
#    Position /[mi]\d+$/ ( @all_above_marks <yPlacement=-90 xPlacement=+50> )  @narrowmedi /[KG]AF/;
#    Position /[mi]\d+$/ ( @all_above_marks <yPlacement=-90 xPlacement=+50> )  /[KG]AF/;
#    Position /[mi]\d+$/ ( @all_above_marks <yPlacement=-50> @all_above_marks <yPlacement=200> ) /[KG]AF/;
#    Position /[mi]\d+$/ ( @all_above_marks <yPlacement=-50> @all_above_marks <yPlacement=200> ) @narrowmedi /[KG]AF/;
#    Position ( @all_above_marks <xPlacement=50> )  /ALIFf|LAM[mf]/;
#    Position ( toeda <15 -90 0 0> )  @kafgaf;
#    Position ( toeda <15 -90 0 0> )  @narrowmedi @kafgaf;
#    Position ( @above_nuktas <15 -60 0 0> )  @kafgaf;
#    Position ( @above_nuktas <15 -60 0 0> )  @narrowmedi @kafgaf;
#    Position ( @other_above_marks <15 -100 0 0> )  @kafgaf;
#    Position ( @other_above_marks <15 -100 0 0> )  @narrowmedi @kafgaf;
#  } UseMarkFilteringSet @all_above_marks;

};

Feature kern {
  Routine GiveChYeDotsMoreSpace {
    Position ( [ddb.yb tdb.yb] <80 -100 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( sdb.yb <75 0  0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( haydb.yb <125 0  0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( haydb.yb <125 0  0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /.yb|tdb|ddb$/ <20 0 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /.yb|tdb|ddb$/ <20 0 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
  } UseMarkFilteringSet /haydb.yb|sdb.yb|tdb|ddb/;

  Routine DropDotsBeforeVao {
    Position ( [tdb ddb] <yPlacement=-100> ) VAOf1;
  } UseMarkFilteringSet /tdb|ddb$/;

  DefineClass @WideFinals = @finas & (xMin < 50);
  Routine RaiseDotsAfterWideFinals {
    Position @WideFinals @inits ( [ddb tdb] <yPlacement=+150> ) ;
  } UseMarkFilteringSet [ddb tdb];

  Routine KernBeHaya {
    Position (space <xAdvance=-100> ) /[BT]Ei/ /HAYA/;
  } IgnoreMarks;

  Routine GrossKasraAvoidanceThing {
    Position [sdb.yb ddb.yb tdb.yb] ( KASRA.yb <yPlacement=-200> ) AINf1;
    Position ( KASRA.yb <yPlacement=+200> ) AINf1;
    Position [sdb.yb ddb.yb tdb.yb] ( KASRA.yb <yPlacement=-400> ) JIMf1;
  };

  Routine FinalJimRaiseDots {
    Position /[mi]\d+$/ [sdb ddb tdb] JIMf1 ([tdi ddi sdi] <yPlacement=150> );
  };

  DefineClass @TallAndStraight = /LAM[im]|[KG]AF[im]|JIM[im]/;
  DefineClass @LowNotTeh = /^(?!TE|LAM|[KG]AF|ALIF)\w+[mf]\d+/;
  #Routine HighTehFinesse {
  #  Position @TallAndStraight /TEm/ ( [tda dda] <xPlacement=-150> ) @LowNotTeh;
  #  Position @TallAndStraight /TEm/ ( [tda dda] <xPlacement=-150> ) /_YEf/;
  #   Position @TallAndStraight @NarrowTeh ( [tda dda] <yPlacement=150> ) @NarrowTeh [tda dda];
  #   Position @TallAndStraight @NarrowTeh ( [tda dda] <yPlacement=300> );
  #}  UseMarkFilteringSet [dda tda];

  Routine TdbAinFinesse {
    Position (tdb.yb <xPlacement=-30>) AINf1;
  } UseMarkFilteringSet [tdb.yb];

  Routine BehHayaFinesse {
    Position /BE(sd)?i/ ([ddb tdb] <xPlacement=50>) /HAYAm/;
  } UseMarkFilteringSet [ddb tdb];

  DefineClass @NotTooHighStuff = (@finas|@isols) & (yMax < 600);
  Routine AddSpaceAroundPointyDamma {
    Position @NotTooHighStuff DAMMA (space <xAdvance=+50> );
  };

  Routine InitChechFinesse {
    Position ( /JIMi/ <xAdvance=+50> tdb <xPlacement=+50> );
  };

  Routine FeTeFinesse {
    Position /FEi/ /TEm/ ( dda <yPlacement=+50> ) ;
    Position /FEi/ dda /TEm/ ( dda <yPlacement=+50> ) ;
  } UseMarkFilteringSet [dda];

  Routine InitKasraYBFinesse { # رِیی
    Position KASRA ( @inits <xAdvance=+200> ) /.yb/;
  };

  Routine AddIsolDammaSpace {
    Position [@isols @finas] DAMMA ( @inits <xAdvance=+100> );
  } UseMarkFilteringSet [DAMMA];

  Routine ClearAlifMadda {
    Position ALIFu1 MADDA ( /i\d/ <xAdvance=+130> ) @all_above_marks;
    Position ALIFu1 MADDA ( /i\d/ <xAdvance=+100> );
  };

  Routine BehJimFinesse {
    Position ( /ddb|tdb/ <xPlacement=+20> ) /JIMm/;
  } UseMarkFilteringSet /ddb|tdb/;

  Routine HehAlifFix {
    Position HAYCi3 ( haydb <yPlacement=+165> ) ALIFf1;
  } UseMarkFilteringSet /haydb/;
};
