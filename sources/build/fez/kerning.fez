LoadPlugin qalamTools.NastaliqKerning;
Include "shared.fez";

DefineClass @bariye = /BARI_YE/;

LoadPlugin FontEngineering;
SetCategory space.urdu ligature;


Feature kern {
	NastaliqKerning 100 70%;

	# Split into two to allow for the first to be class-based
	Routine FixShortKern {
		Position /(MIM|RE|VAO)[uf]/ (/[KG]AFi|LAMi|TEi|BEi|FEi/ <xAdvance=+450>) /[sdt]db/ /CH_YEf|JIMf|AINf|MIMf/;
		Position /(MIM|RE|VAO|DAL)[uf]/ (/LAMi/ <xAdvance=+700>) /CH_YEf|JIMf|AINf|MIMf/;
		Position /(MIM|RE|VAO|DAL)[uf]/ (/[KG]AFi|TEi|BEi|FEi/ <xAdvance=+550>) /CH_YEf|JIMf|AINf|MIMf/;
		Position (/[KG]AFi|LAMi|TEi|BEi|FEi/ <xAdvance=+200>) /[sdt]db/ /CH_YEf|JIMf|AINf|MIMf/;
	} IgnoreLigatures UseMarkFilteringSet /[sdt]db/;
	Routine FixShortKern2 {
		Position /ALIF/ (/[KG]AFi|LAMi|TEi|BEi|FEi|MIMi/ <xAdvance=+100>) /CH_YEf|JIM[fm]|AINf|MIMf/;
		Position /ALIF/ (/[KG]AFi|LAMi|TEi|BEi|FEi|MIMi/ <xAdvance=+60>);
		Position /(DAL|RE|VAO|ALIF)[uf]/ (/[KG]AFi|LAMi/ <xAdvance=+300>) /(JIM|BE)m/ /[sdt]db/;
	} IgnoreLigatures UseMarkFilteringSet /[sdt]db/;
	Routine FixShortKern3 {
		Position /ALIF/ (/TEi/ <xAdvance=+100>) /dda/;
		Position /REf3/ (/TEi/ <xAdvance=+100>);
		Position /REu1|VAOf1|VAOu1/ (/BEi3|BEisd1/ <xAdvance=+100>);
	} IgnoreLigatures UseMarkFilteringSet /dda/;
	Routine FixLongKern {
		Position REf3 (/[KG]AFi/ <xAdvance=-120>);
	};
};
