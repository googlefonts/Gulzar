LoadPlugin karakul.NastaliqKerning;
Include "shared.fez";

# We change the space.urdu category to "ligature", so that we can
# kern across spaces by using the IgnoreLigatures flag.
LoadPlugin FontEngineering;
SetCategory space.urdu ligature;
SetCategory comma.urdu ligature;

DefineClass @narrowmedi = /.*m\d+$/  & (run < 350);

Routine NastaliqKerning {
    NastaliqKerning 120 65%;
} IgnoreMarks IgnoreLigatures;

Feature kern {
	# This runs the autokerner, and brings together initial glyphs
	# and final glyphs to within either 90 units of one another or
	# to a maximum of 75% of the final glyph's width "under" the
	# final glyph, whichever is the smallest.
	# We don't kern bari-yes, and we have to include BARI_YEu1 in that.
	DefineClass @bariye = /BARI_YE/;
    Routine RunKerner {
    		# We should be able to contextualise this more, but it's not
    		# working and I don't understand why not.
        Chain ([@isols @finas] ^NastaliqKerning);
    } IgnoreMarks IgnoreLigatures;

	# The autokerner only considers the space underneath two glyphs:
	# the final/isolate of one "word" and the initial of the next. But
	# if the initial is followed by another letter which "curls back"
	# into the space beneath the initial (for example, a JIM), then
	# the final can actually collide with the medial. We don't expect
	# the autokerner to detect this, and deal with it manually.
	Routine FixShortKern {
		Position [@isols @finas] (/([KG]AF|LAM)i/ <xAdvance=+100>) /JIMm|BEm14/;
		Position [@isols @finas] (/([KG]AF|LAM)i/ <xAdvance=+100>) /BEm9/;
		Position [REf2 REf3] ([FEi13 GAFi13 KAFi13 BEi13 TEi13 AINi13  LAMi13 LAMi2 LAMi9] <xAdvance=+270>) @finas; # دبی
		Position REu1 ([FEi13 GAFi13 KAFi13 BEi13 TEi13 AINi13  LAMi13 LAMi2 LAMi9] <xAdvance=+250>) @finas; # دبی
		Position REf1 ([FEi13 GAFi13 KAFi13 BEi13 TEi13 AINi13  LAMi13 LAMi2 LAMi9] <xAdvance=+200>) @finas; # دبی
		Position @dal_like ([AINi13] <xAdvance=+100>) @finas; # دبی
		Position @vao_like ([AINi13] <xAdvance=+100>) @finas; # عمومی
		Position [@vao_like @dal_like] ([JIMi13] <xAdvance=+50>) @finas; # فوجی
		Position [@vao_like @dal_like] ([MIMi13] <xAdvance=+150>) @finas; # گرمی
		Position @dal_like ([FEi13 GAFi13 KAFi13 BEi13 TEi13 LAMi13 LAMi2 LAMi9] <xAdvance=+220>) @finas; # دبی
		Position @vao_like ([FEi13 GAFi13 KAFi13 BEi13 TEi13 LAMi13 LAMi2 LAMi9] <xAdvance=+220>) @finas; # وبی
		Position @mim_like ([FEi13 GAFi13 KAFi13 BEi13 TEi13 LAMi13 LAMi2 LAMi9] <xAdvance=+160>) @finas; # م بی
		Position @alif_like ([BEi13 TEi13 AINi13  LAMi13] <xAdvance=+150>) @finas;
		Position @alif_like ([FEi13 GAFi13 KAFi13 LAMi2 LAMi9] <xAdvance=+120>) @finas; #  
		Position @alif_like ([GAFi17 KAFi17] <xAdvance=+150>) @finas; # کجاکخ
		Position @alif_like ([LAMi9] <xAdvance=+100>) /MIMm/; # المللی
		Position [@isols @finas] ([FEi13 GAFi13 KAFi13 BEi13 TEi13 AINi13  LAMi13 LAMi2 LAMi9] <xAdvance=+200>) @finas; # الی

		Position [@isols @finas] ([BEi1 TEi1 FEi1 FEi7] <xAdvance=+170>) @finas; # سابع
		Position [@isols @finas] ([JIMi7] <xAdvance=+100>) @finas; # أحج
		Position [@isols @finas] ([LAMi7] <xAdvance=+250>) JIMf1; # صالح
		Position [@isols @finas] (@inits <xAdvance=+100>) @narrowmedi AINf1; # صالح
		Position [@isols @finas] ([GAFi5 KAFi5] <xAdvance=+100>) HAYAf1; # رکھ

		Position ([BEisd1 TEisd1]  <xAdvance=+100>) TEm7 JIMf1;
		Position /(CH_YE)[uf]/ ([BEi13 TEi13 LAMi11 LAMi2 LAMi9] <xAdvance=+300>); # یی پی

		Position ([BEi7 TEi7] <xAdvance=+100>) JIMf1;
	} IgnoreMarks;

	# And finally some ad-hoc fixes...

	Routine OtherKerningFixes {
		Position @bigbowlfina space.urdu (/([KG]AF|LAM)i/ <xAdvance=+50>) /JIMm|BEm/ [ddb tdb];
		Position REf3 (/[KG]AFi/ <xAdvance=-120>);
		Position @vao_like (space.urdu <xAdvance=50>) /AINi/;
		Position /NUN[uf]/ (space.urdu <xAdvance=100>) /HAYCi/;
		Position [REf2 REf3] (ALIFu1 <xAdvance=200>) [BEi11 TEi11];
		Position [REf2 REf3] (SADu1 <xAdvance=200>); # عرض
		Position @dal_like (ALIFu1 <xAdvance=-50>); # جدا
	} UseMarkFilteringSet /[sdt]db/;
};
