LoadAnchors;
LoadPlugin qalamTools.CopyAnchors;
CopyAnchors SHADDA SHADDA_DAMMA;
CopyAnchors SHADDA SHADDA_KASRA;
CopyAnchors SHADDA SHADDA_FATHA;
CopyAnchors SHADDA SHADDA_LONG_A;

LoadPlugin qalamTools.QuantizeAnchors;
LoadPlugin qalamTools.DotAvoidance;
LoadPlugin qalamTools.NastaliqKerning;

AddSpacedAnchors 220;

Include "shared.fez";


Feature curs {
  Routine CursiveAttachment { Attach &entry &exit cursive; } IgnoreMarks RightToLeft;
};

QuantizeAnchors 10;

Feature mark {
  Routine DoMarkBase {
    Attach &top &_top bases;
    Attach &top.one &_top.one bases;
    Attach &top.two &_top.two bases;
    Attach &inside &_inside bases;
    Attach &bottom &_bottom bases;
    Attach &bottom.one &_bottom.one bases;
    Attach &bottom.two &_bottom.two bases;
    Attach &comma &_comma bases;
    Attach &comma.one &_comma.one bases;
  };
  Routine ReattachToeda {
    Attach &toeda &_toeda [REf2 REf3];
  };
};


Routine DropOne {
  Substitute [sdb ddb tdb haydb] -> $1.one;
  Substitute [sdb.yb ddb.yb tdb.yb haydb.yb] -> $1~yb.one;
};

Routine DropTwo {
  Substitute [sdb ddb tdb] -> $1.two;
  # Substitute [sdb.yb ddb.yb tdb.yb ] -> $1~yb.two;
};

# Positioning rules happen *after* dot avoidance, so need to match .one and .two forms
Routine OpenSpaceBeforeKern {
  Position (@inits <xAdvance=+300>) /tdb|haydb/;
  Position (@inits <xAdvance=+200>) /sdb|ddb/;
  Position toeda (@inits <xAdvance=+200>);
} IgnoreLigatures UseMarkFilteringSet /db|toeda/;

Routine OpenSmallSpaceBeforeKern {
  Position (/JIMi/ <xAdvance=+250>) /tdb/;
  Position (@inits <xAdvance=+160>) /db|[mf](sd)?\d+/;
  Position toeda (@inits <xAdvance=+150>);
} IgnoreLigatures UseMarkFilteringSet /db|toeda/;

Routine OpenTinySpaceBeforeKern {
  Position (/JIMi/ <xAdvance=+150>) /tdb/;
  Position (@inits <xAdvance=+100>) /db|[mf](sd)?\d+/;
}  UseMarkFilteringSet /db/;

Routine DoNothing    {Substitute JIMi1 -> JIMi1; };
Routine DoNothingPos {Position (haydb.yb <xPlacement=0>); };
Routine Tighten  { Position (@inits <xAdvance=-150>); };
Routine Raise    { Position ([sdb tdb ddb] <yPlacement=+100>); };
Routine TopRight {Position ([sdb tdb ddb] <yPlacement=+250 xPlacement=+150>); };
Routine BottomLeft {Position ([sdb tdb ddb] <yPlacement=-250 xPlacement=-150>); };
Routine LeftABit {Position ([sdb tdb ddb] <xPlacement=-100>); };

Routine DropATinyBitMore {
  Position ([tdb.one ddb.one tdb.yb sdb.yb ddb.yb] <yPlacement=-150>);
  Position ([tdb.two sdb.two ddb.two] <yPlacement=-220>);
};

Routine DropALotMore {
  Position ([tdb.one ddb.one tdb.yb sdb.yb ddb.yb] <yPlacement=-200>);
  Position ([tdb.two sdb.two] <yPlacement=-270>);
};

Routine ClearRehDots {
  Chain ([REf1 REf2 REf3] [tda sda toeda] @inits ^OpenSmallSpaceBeforeKern);
} IgnoreLigatures;



# The real stuff starts now.
DefineClass @dots = [sdb ddb tdb haydb];

Routine AtHeight0sub {
  Chain (REf1                BEi10                  ^DoNothing);

} IgnoreLigatures UseMarkFilteringSet [sdb ddb tdb];


Routine AtHeight100sub {
  Chain ([REf2 REf3]          @BEi      @dots        ^DropOne); # جر یا / سرپہ
  Chain ([REf2 REf3]          @JIMi     @dots        ^DropTwo); # ہر چیز / ہر چیز
  Chain ([REf2 REf3]          /[TB]Ei3/ @DOTm @dots   ^DropOne); # پھرتیر
  Chain ([REf2 REf3]          ALIFu1 @DOTi     @dots        ^DropOne); # یئر ایپ

  Chain (@dal_like            BEi3      @dots        ^DoNothing); # ربا
  Chain (@dal_like            @BEi      @dots        ^DropOne); # پرچا
  Chain (@dal_like            @JIMi     @dots        ^DropOne); # پرچا

  Chain (@vao_like            BEi3      @dots        ^DoNothing); # ہ پا
  Chain (@vao_like            @DOTi     @dots        ^DropOne); # وپہر
  Chain (HAYCf1               BEi3      @dots        ^DoNothing); # ہہ پا
  Chain (HAYCf1               @DOTi     @dots        ^DropOne); # ہہ چہر

  Chain (@kaf_like            @DOTi     @dots        ^DropOne); # یپ چل
  Chain (@bigbowlfina         JIMi3     tdb          ^DropOne); # فی چا

  Chain [REf3 REf3] (@alif_like  BEisd2 @dots        ^DropOne); # یئر ایپ

} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb];

Routine AtHeight200sub {
  Chain (@dal_like            HAYCi3    @dots        ^DoNothing); # رہیں
  Chain [REf2 REf3] (@dal_like @DOTi     @dots        ^DropTwo); # نمبرز پر
  Chain (@dal_like            @DOTi     @dots        ^DropOne); # دچھ
  Chain (@dal_like    /FEi/   /(BE|JIM)m/ @dots        ^DropOne); # رفین
  Chain (@dal_like    @BEi    /(BE|JIM)m/ @dots        ^DropOne); # رنیست

  Chain (@vao_like            BEisd1    @dots        ^DoNothing); # وپید
  Chain (@vao_like            BEi6      @dots        ^DropTwo); # کر یہُو
  Chain (@vao_like            @BEi      @dots        ^DropOne); # ھو یں
  Chain (@vao_like            @JIMi     @dots        ^DropOne); # ھو چں
  Chain (@vao_like            HAYCi11   @dots        ^DropOne); # ساتھ ہی
  Chain (@vao_like            @DOTi     /.yb$/        ^DropTwo); # بلوچی

  Chain ([REf2 REf3]          BEi3  @DOTm @dots      ^DoNothing); # بغیر ٹیبلٹ
  Chain ([REf2 REf3]          JIMi1 @DOTm @dots      ^DoNothing); # سرخیوں
  Chain ([REf2 REf3]          BEisd1 @DOTm @dots     ^DoNothing); # سرٹیفکیٹ 

  Chain ([REf2 REf3]          BEisd2 @DOTm @dots     ^DropOne); # نٹرنیٹ

  Chain ([REf2 REf3]          @JIMi     sdb          ^DoNothing); # but not سر جہا
  Chain ([REf2 REf3]          BEi12    @dots         ^DropOne); # جھر پو
  Chain ([REf2 REf3]          @DOTi     @dots        ^DropTwo); # سرپر  / سربر

  Chain (HAYCf1               @DOTi     @dots        ^DropOne); # یہ بہت

  Chain (BEf1 @dots           BEi6     @dots        ^DropOne); # جب یہُو

  Chain (TEf1               /HAYCi/    @dots        ^DropOne); # کت ہو
  # Chain (@kaf_like            @JIMi     @dots        ^DropOne); # یپ چل
  #Chain (@kaf_like @dots      @JIMi     @dots        ^DropOne); # یپ چل

  Chain (@alif_like           BEi13     @dots        ^DropOne); # اپر

} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb tdb.yb sdb.yb ddb.yb];

Routine AtHeight400sub {
  #Chain (@dal_like            JIMi5     @dots        ^DropTwo); # پرچھا
  Chain (@dal_like            BEi3     sdb        ^DoNothing); # ر بلتی 
  Chain (@dal_like            BEisd1    @dots  @DOTm /.yb$/ ^DoNothing); # قریبی
  Chain (@dal_like            BEisd1    @dots        ^DropOne); # دیتی

  Chain (@dal_like            @DOTi     @dots        ^DropTwo); # پریھا / پرچھا / رچینی

  Chain @dots (@vao_like      @DOTi     @dots        ^DropTwo); #  پوچھا

  Chain (@vao_like            @BEi     @dots        ^DropOne); # ویکیپیڈ but not کو ہَیکل
  Chain (@vao_like            @JIMi     @dots        ^DropOne); # ویکیپیڈ

  Chain (HAYCf1               @DOTi     @dots        ^DropTwo); # ہمیشہ چلتی

  Chain (@kaf_like            @DOTi     @dots        ^DropTwo); # ک پہلو
  Chain (@kaf_like @dots      @DOTi     @dots        ^DropTwo); # سپ پہلو
  Chain (BEu1 @dots           @DOTi     @dots        ^DropTwo); # پ پہلو


} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb sdb.yb ddb.yb tdb.yb];

Routine AtHeight500sub {
  Chain (@vao_like            @DOTi     [ddb tdb]        ^DropTwo); # لو چھیا / و چھیا / but not و جھی

  Chain (@dal_like            BEi9      @dots            ^DropOne); # ریم
  Chain (@dal_like            BEi13     @dots            ^DropOne); # پریھا

  # Decisions, decisions...
  Chain (@dal_like            BEi2      @dots            ^DropOne);   # ریعقُو
  Chain (@dal_like            JIMi9     @dots            ^DropTwo);   # ر چمکتا
  Chain (@dal_like            BEi3      @dots            ^DropTwo);   # دیکھتا
  Chain (@dal_like            BEisd1    @dots            ^DropOne);   # Similar shape to دیکھتا
  Chain (@dal_like            BEi5      @dots            ^DoNothing);   # ر پھر
  Chain (@dal_like            BEi6      @dots            ^DoNothing);   # کو پہنچ
  Chain (@dal_like            HAYCi9    @dots            ^DropOne);   # کو پہنچ
  # Chain (@dal_like            BEi5      @dots            ^DropTwo); # رپھر

  Chain (@dal_like            @DOTi     @dots            ^DoNothing); # ندبجن (space instead)

  Chain (HAYCf1               BEi7      @dots            ^DoNothing); # یہ بجشا / be careful with یہ پجشا
  Chain (HAYCf1               BEi5      @dots            ^DropTwo); # قہ پھیلا
  Chain (HAYCf1               BEisd1    @dots            ^DropTwo); # سلہ پیکٹس
  Chain (HAYCf1               @DOTi      @dots            ^DropOne); # لہ پیکٹس / کہ ہمیشہ

  Chain (@kaf_like            @DOTi     @dots            ^DropTwo); # تک پہنل
  Chain (@kaf_like @dots      @DOTi     @dots            ^DropTwo); # پ یعقر

} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb];


Routine AtHeight600sub {
  Chain (@vao_like            JIMi5     @dots            ^DoNothing); # وچھی
  Chain (@vao_like            BEi6      @dots            ^DoNothing); # کو پہنچ
  Chain (@vao_like            JIMi9     @dots            ^DropTwo);   #  وجمھ
  Chain (@vao_like            @DOTi     [ddb tdb]        ^DropTwo);   # لو چھیا / و چھیا / but not و جھی
  Chain (@vao_like            /FEi/     @DOTm @dots  ^DoNothing);       # مینوفیکچر
  Chain (@vao_like            @inits     @DOTm @dots  ^DropOne);       # کمیونیکیشن / ومچھا


  Chain (@dal_like            BEi9      @dots            ^DropTwo);   # ریم / دیمس
  Chain (@dal_like            BEi13     @dots            ^DropOne);   # پریھا
  Chain (@dal_like            HAYCi9    @dots            ^DropOne);   # رہم
  Chain (@dal_like            @inits    BEm9 @dots  ^DoNothing);       # ترمیم 
  Chain (@dal_like            @inits     @DOTm @dots  ^DropOne);       # کمیونیکیشن / دمچھا

  # Decisions, decisions...
  Chain (@dal_like            BEi5      @dots            ^DoNothing);   # ر پھر
  Chain (@dal_like            BEi6      @dots            ^DoNothing);   # کو پہنچ
  # Chain (@dal_like            BEi5      @dots            ^DropTwo); # رپھر

  Chain (@dal_like            @DOTi     @dots            ^DoNothing); # ندبجن (space instead)

} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb];

# Rules which use kerning instead of dropping
Routine AtHeight0pos {
  Chain (@bigbowlfina        [BEisd1 BEisd2]   ^OpenSpaceBeforeKern [ddb tdb]); # ان پٹ
  Chain (@bigbowlfina        @BEi   ^OpenSmallSpaceBeforeKern [ddb tdb]); # کی پا
  Chain ([REf1 REf2 REf3] ^ClearRehDots); # یکنز شا
} IgnoreLigatures UseMarkFilteringSet /sdb|ddb|tdb|haydb|toeda/;

Routine AtHeight200pos {
  #Chain (@bigbowlfina        [BEisd1 BEisd2] ^OpenSpaceBeforeKern);
  Chain (@bigbowlfina        BEi13   ^OpenTinySpaceBeforeKern); # کی پر
  Chain (@bigbowlfina dda    BEi13   ^OpenTinySpaceBeforeKern); # کی پر
  Chain (@bigbowlfina        @BEi   ^OpenSpaceBeforeKern); # عمل پو
  Chain (@bigbowlfina dda    @BEi   ^OpenSpaceBeforeKern); # عمل پو
  Chain (@bigbowlfina        @JIMi   ^OpenSpaceBeforeKern); # عمل پو # Not HAYCi
  Chain (@bigbowlfina dda    @JIMi   ^OpenSpaceBeforeKern); # عمل پو # Not HAYCi
  Chain (@bigbowlfina        /HAYCi/   ^OpenSmallSpaceBeforeKern); # ن ہو
  Chain (@bigbowlfina dda    /HAYCi/   ^OpenSmallSpaceBeforeKern); # ن ہو

  Chain (@kaf_like           @DOTi   ^OpenSmallSpaceBeforeKern [ddb tdb haydb]); # پ چل
  Chain (@kaf_like toeda     @DOTi   ^OpenSmallSpaceBeforeKern [ddb tdb haydb]); # پ چل
  Chain (@kaf_like @dots     @DOTi   ^OpenSmallSpaceBeforeKern [ddb tdb haydb]); # پ چل

  Chain @dots (@dal_like @BEi ^OpenSmallSpaceBeforeKern /sdb|ddb/); # پذیر
  Chain (@dal_like           [BEi3 TEi3] ^OpenSmallSpaceBeforeKern   /(BE|JIM)m/ @dots); # ر نیس / رتیں
  Chain (@dal_like           BEi6    /sdb|ddb|tdb|haydb|toeda/ ^DropATinyBitMore); # کر یہُو
  Chain (@dal_like           HAYCi3  ^OpenSmallSpaceBeforeKern); # رہیں?
  Chain (@dal_like  toeda    @inits  ^OpenSmallSpaceBeforeKern); # یڈشیٹ / یڈیو


  Chain (@mim_like           BEi12   ^OpenSpaceBeforeKern);
  Chain (@mim_like           @DOTi   ^OpenSpaceBeforeKern);

  Chain (REf1                BEi10   /sdb|ddb|tdb|haydb|toeda/ ^DropATinyBitMore); # تقریظیں
  Chain ([REf2 REf3] [sda toeda]   /HAYA/ ^OpenTinySpaceBeforeKern); # گھڑھا
  Chain ([REf2 REf3] [sda toeda] @inits ^OpenSmallSpaceBeforeKern); # گھڑسو
  Chain @dots ([REf2 REf3]   @BEi    /sdb|ddb|tdb/ ^DropATinyBitMore); # متجرپھ
  Chain ([REf2 REf3]         BEi12   /sdb|ddb|tdb|haydb|toeda/ ^DropATinyBitMore); # جھر پو
  Chain ([REf2 REf3]         BEi3    /sdb|ddb|tdb|haydb|toeda/ ^DoNothingPos); # مر یا
  Chain ([REf2 REf3]         BEisd1  ^OpenTinySpaceBeforeKern); # پھر پیا
  Chain ([REf2 REf3]         BEisd2  /sdb|ddb|tdb|haydb|toeda/ ^DropATinyBitMore); # تحریریں
  Chain ([REf2 REf3]         @DOTi ^OpenTinySpaceBeforeKern @DOTm @dots      ); # نٹرنیٹ
  Chain ([REf2 REf3]         @BEi    ^OpenSmallSpaceBeforeKern    /sdb|ddb|tdb|haydb|toeda/ ^DropATinyBitMore @DOTm @dots); # تحریریں

  Chain ([REf2 REf3]         @BEi    ^OpenTinySpaceBeforeKern /\.yb/ ^DropATinyBitMore); # تحریریں ! / متجرپھ
  Chain ([REf2 REf3]         @BEi    ^OpenTinySpaceBeforeKern); # تحریریں

  Chain (@alif_like          @BEi    ^OpenTinySpaceBeforeKern dda);
  Chain [REf2 REf2] (@alif_like @BEi  [sdb.yb ddb.yb tdb.yb] ^DropATinyBitMore); # خرابی
} IgnoreLigatures UseMarkFilteringSet /sdb|ddb|tdb|haydb|toeda|dda/;

Routine AtHeight400pos {
  Chain (@bigbowlfina        @DOTi   ^OpenSmallSpaceBeforeKern);  # کی پیما / کی پیمائ
  Chain ([REf2 REf3]         BEi1    ^OpenSpaceBeforeKern);  #   کیلیبریشن
  Chain ([REf2 REf3]         BEi4    ^OpenSmallSpaceBeforeKern);  #  طریقو

  Chain (@mim_like           @DOTi   ^OpenSmallSpaceBeforeKern);

  Chain (@dal_like            BEisd1 ^OpenSpaceBeforeKern @dots ^Raise @DOTm /.yb$/ ); # قریبی
  Chain (@dal_like           BEi3    ^OpenSmallSpaceBeforeKern); # ربلتی  ر بلتی 
  Chain (@dal_like           BEi3    ^OpenSmallSpaceBeforeKern); # ربلتی  ر بلتی 
  Chain (@dal_like           /HAYCi/ ^OpenSmallSpaceBeforeKern);       #  رہنما
  Chain ddb (@dal_like @DOTi ^OpenSmallSpaceBeforeKern);       #  ایدیشن
  Chain (@dal_like  toeda    @inits  ^OpenSmallSpaceBeforeKern); # یڈشیٹ / یڈیو

  Chain tdb (@vao_like      @DOTi ^OpenTinySpaceBeforeKern); #  پوچھا
  Chain (@vao_like           BEi6    ^OpenSmallSpaceBeforeKern);  #  کو پہن
  Chain (@vao_like           /HAYCi/ ^OpenSpaceBeforeKern);       # کو ہَیکل

  Chain (@alif_like          JIMi5   ^OpenTinySpaceBeforeKern @dots);  # اچھا
  Chain (@alif_like          @BEi   ^OpenTinySpaceBeforeKern @dots);  # نلایتی

  Chain (@kaf_like           JIMi3   ^OpenSmallSpaceBeforeKern @dots);  # لت جیسی

} IgnoreLigatures UseMarkFilteringSet /sdb|ddb|tdb|haydb|toeda/;

Routine AtHeight500pos {
  Chain (@bigbowlfina        @DOTi   ^OpenSmallSpaceBeforeKern);  # کی پیچید
  Chain (@dal_like           JIMi5   ^OpenSpaceBeforeKern);       # رجھر
  Chain (@dal_like toeda     @inits  ^OpenSpaceBeforeKern);       # ڈ مستقل
  
  # XXX prefer to raise dots
  Chain (@dal_like           BEi1    ^OpenSpaceBeforeKern);   # ریسپشن

  Chain (@kaf_like           @BEi    ^OpenSpaceBeforeKern @dots);       # تک پہنچنے

  Chain (@mim_like           @DOTi   ^OpenSmallSpaceBeforeKern);

  Chain (@vao_like           @JIMi   ^OpenSmallSpaceBeforeKern @dots @DOTm @dots);
  Chain (@vao_like           @JIMi   @dots ^LeftABit); # کوجسیی
  Chain (@vao_like           BEi5    ^OpenSmallSpaceBeforeKern);

  Chain (HAYCf1              BEi7    ^OpenSmallSpaceBeforeKern  @dots ^TopRight); # یہ بجشا
  Chain (HAYCf1              @BEi   ^OpenSmallSpaceBeforeKern @dots); # فہ پھیلا

} IgnoreLigatures UseMarkFilteringSet /sdb|ddb|tdb|haydb|toeda/;

Routine AtHeight600pos {
  Chain ([@isols @finas] @inits ^DoNothingPos [JIMf1 AINf1]); # ریخ
  Chain ([@isols @finas] @inits ^DoNothingPos @dots [JIMf1 AINf1]); # ریخ
  # XXX  @DOTm
  Chain (@bigbowlfina        @DOTi   ^OpenSpaceBeforeKern);       # ی چھو

  Chain (@alif_like          @BEi    ^OpenSmallSpaceBeforeKern @dots);  # ایپلیکیشنز
  Chain (@alif_like          @JIMi   ^OpenSpaceBeforeKern @dots);  # اچھّی

  Chain (@kaf_like            JIMi5     @dots            ^TopRight); # ید چھر, getting fancy
  Chain (@kaf_like [@dots toeda]      JIMi5     @dots            ^TopRight); # 
  Chain (@kaf_like            @BEi    ^OpenSmallSpaceBeforeKern tdb); # یٹ پیکج
  Chain (@kaf_like [@dots toeda] @BEi    ^OpenSmallSpaceBeforeKern tdb);

  Chain (@dal_like           BEi5    ^OpenSpaceBeforeKern);       # ر پھر
  Chain (@dal_like           BEi7    ^OpenSpaceBeforeKern);       # ریخی
  Chain (@dal_like           BEi9    ^DoNothingPos);              # فدیم
  Chain (@dal_like           @BEi    ^OpenSmallSpaceBeforeKern);  # ندبجن /  دیکھیں / ریسپشن

  Chain (@dal_like           JIMi5   ^OpenSpaceBeforeKern);       # ورجھر

  Chain (@mim_like           @DOTi   ^OpenSmallSpaceBeforeKern @dots);  # م ہم

  # For soe reason I put open small space here?
  Chain (@vao_like           BEi5 [sdb ddb] ^BottomLeft); # ہ بھی

  Chain (@vao_like           [BEisd1]  [ddb.two tdb.two] ^DropATinyBitMore); # ویجٹس
  Chain (@vao_like           [BEisd1]  ^OpenSmallSpaceBeforeKern); # کمیونیکیشن / ہ بجی
  Chain (@vao_like           HAYCi9  ^OpenSpaceBeforeKern);       # وہم
  Chain (@vao_like           JIMi5   ^OpenSpaceBeforeKern) tdb;   # وچھی
  Chain (@vao_like           JIMi5   ^OpenSmallSpaceBeforeKern);  # و جھی
  Chain (@vao_like           /MIMi/  ^OpenSmallSpaceBeforeKern);  # جومُجھ
  Chain (@vao_like           BEi6    ^OpenSmallSpaceBeforeKern);  # کو پہنچ
  Chain (@vao_like           BEi5                                 /sdb|ddb|tdb|haydb/ ^DropATinyBitMore); # وپھی
  Chain (@vao_like           BEi7                                 /sdb|ddb|tdb|haydb/ ^DropATinyBitMore); # ویجٹس

  Chain (REf3                BEi5    ^OpenSpaceBeforeKern);       # سرپھر

  Chain (@kaf_like           BEi6    ^Tighten);                   # تک پہنچن
  Chain (@kaf_like           JIMi5  /sdb|ddb|tdb|haydb/ ^DropATinyBitMore); # ک چھو
  Chain (HAYCf1              /HAYCi/ ^OpenSpaceBeforeKern);       # گ ہم

  Chain (HAYCf1              BEi5    ^OpenSmallSpaceBeforeKern @dots); # قہ پھیلا
  Chain (HAYCf1              JIMi5   @dots            ^Raise);     # صفحہ چھو
  Chain (HAYCf1              HAYCi3  haydb            ^DoNothingPos); # 

} IgnoreLigatures UseMarkFilteringSet /sdb|ddb|tdb|haydb|toeda/;

Feature rlig {
  #AtHeight 0-99 AtHeight0sub;
  AtHeight 100-199 AtHeight100sub;
  AtHeight 200-399 AtHeight200sub;
  AtHeight 400-499 AtHeight400sub;
  AtHeight 500-599 AtHeight500sub;
  AtHeight 600-800 AtHeight600sub;
  DetectAndSwap top reverse;
  DetectAndSwap bottom;
  DetectAndSwap top;
  Routine OtherFixes {
    Substitute FEi3 (dda) ALIFf1 -> dda.one;
    Substitute /FEi/ sda /FEm/ (dda.one) -> dda.two;
    Substitute /FEi/ dda /FEm/ (sda) -> sda.two;
  };
  Routine OtherFixes2 {
    Chain ([REf1 REf2 REf3] [tda sda toeda] @isols ^OpenSmallSpaceBeforeKern);
  };
};

Feature kern {
  AtHeight 0-199 AtHeight0pos;
  AtHeight 200-399 AtHeight200pos;
  AtHeight 400-499 AtHeight400pos;
  AtHeight 500-599 AtHeight500pos;
  AtHeight 600-800 AtHeight600pos;
};

DefineClass @jim_medis = /m7/;
DefineClass @ain_medis = /m2/;

Feature mkmk {
  Routine LiftJimYBDotsMore {
    Position  /.yb$/ @jim_medis ( /.yb$/ <300 +650 0 0> ) [JIMf1];
    Position ( /.yb$/ <300 +650 0 0> ) /.yb$/ [JIMf1];
    Position ( /.yb$/ <300 +650 0 0> ) [JIMf1];
    Position  /.yb$/ @ain_medis ( /.yb$/ <40 +450 0 0> ) [AINf1];
    Position ( /.yb$/ <40 +450 0 0> ) /.yb$/ [AINf1];
    Position ( /.yb$/ <40 +450 0 0> ) [AINf1];
  } UseMarkFilteringSet /b.yb$/;

  Routine DropJimYBYBDots {
    Position  ( sdb.yb <yPlacement=-80> ) @jim_medis /.yb$/ [JIMf1];
    Position  ( /db.yb$/ <yPlacement=-180> ) @jim_medis /.yb$/ [JIMf1];
  } UseMarkFilteringSet /b.yb$/;

};

Feature mkmk {
  Routine DoMarkAttachment {
    Attach &top &_top marks;
    Attach &bottom &_bottom marks;
    Attach &bottom.yb &_bottom.yb marks;
  };
};

