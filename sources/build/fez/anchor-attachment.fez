LoadAnchors;
LoadPlugin qalamTools.CopyAnchors;
CopyAnchors SHADDA SHADDA_DAMMA;
CopyAnchors SHADDA SHADDA_KASRA;
CopyAnchors SHADDA SHADDA_FATHA;
CopyAnchors SHADDA SHADDA_LONG_A;

LoadPlugin qalamTools.QuantizeAnchors;
LoadPlugin qalamTools.DotAvoidance;
LoadPlugin qalamTools.NastaliqKerning;

AddSpacedAnchors 220;

Include "shared.fez";


Feature curs {
  Routine CursiveAttachment { Attach &entry &exit cursive; } IgnoreMarks RightToLeft;
};

QuantizeAnchors 10;

Feature mark {
  Routine DoMarkBase {
    Attach &top &_top bases;
    Attach &top.one &_top.one bases;
    Attach &top.two &_top.two bases;
    Attach &inside &_inside bases;
    Attach &bottom &_bottom bases;
    Attach &bottom.one &_bottom.one bases;
    Attach &bottom.two &_bottom.two bases;
    Attach &comma &_comma bases;
    Attach &comma.one &_comma.one bases;
  };
};

Routine DropOne {Substitute [sdb ddb tdb haydb] -> $1.one; };
Routine DropTwo {Substitute [sdb ddb tdb] -> $1.two; };

# Positioning rules happen *after* dot avoidance, so need to match .one and .two forms
Routine OpenSpaceBeforeKern {
  Position (@inits <xAdvance=+300>) /tdb|haydb/;
  Position (@inits <xAdvance=+200>) /sdb|ddb/;
  Position toeda (@inits <xAdvance=+200>);
};

Routine OpenSmallSpaceBeforeKern {
  Position (@inits <xAdvance=+160>);
};


Routine DropATinyBitMore {
  Position ([tdb.one ddb.one] <yPlacement=-120>);
  Position ([tdb.two sdb.two ddb.two] <yPlacement=-180>);
};

Routine DoNothing {
  Substitute JIMi1 -> JIMi1;
};

Routine DoNothingPos {
  Position (haydb.yb <xPlacement=0>);
};

DefineClass @bigbowlfina = /(AIN|CH_YE|JIM|LAM|NUN|QAF|SAD|SIN)[fu]/;

Routine AntiKern0sub {
  Chain (REf3 /JIMi/ sdb ^DoNothing);
  Chain (REf1 BEi10 ^DoNothing);
  Chain (REf3 BEi3 [sdb ddb tdb] ^DropOne); # سرپہ
  Chain (REf3 /BEi/ [sdb ddb tdb] ^DropTwo);
  Chain (/(RE|VAO|DAL)f|VAOu/ /BEi|JIMi/ [sdb ddb tdb] ^DropOne);
  Chain (REf3 /BEi|JIMi/ /BEm/ [sdb ddb tdb] ^DropOne);
} IgnoreLigatures UseMarkFilteringSet [sdb ddb tdb];

Routine AntiKern100sub {
  Chain (/NUN|SIN|LAM/ [sdb ddb tdb haydb] /(JIM)i/ ^DoNothing [sdb ddb tdb haydb]); # Do not drop here, space instead
  Chain (/NUN|SIN|LAM/ /(JIM)i/ ^DoNothing [sdb ddb tdb haydb]); # Do not drop here, space instead
  Chain (/(NUN|SIN|LAM)[fu]/ /BEi/ ^DoNothing sdb); # Do not drop here, it's fine
  Chain (REf3 /JIMi/ sdb ^DoNothing);
  Chain ([REf2 REf3] BEi3 [sdb ddb tdb] ^DropOne); # سرپہ

  Chain (REf1 /JIMi/ [sdb ddb tdb] ^DropOne);

  #  ہر چیز
  Chain (REf3 /JIMi/ [ddb tdb] ^DropTwo);
  # وپہر
  Chain (/(VAO|DAL)[fu]/ /BEi/ [sdb ddb tdb] ^DropOne);

  # Unfortunate double drop...
  Chain (/(BE)[fu]\d+/ [sdb ddb ddb.one tdb haydb] /(BEi|JIMi|HAYCi(1|2|4|5|6|7|8|9|10|11|12|13|14)$)/ [sdb ddb ddb.one tdb haydb] ^DropOne);
} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb];

Routine AntiKern200sub {
  Chain (/NUN|SIN|LAM/ [sdb ddb tdb haydb] /(JIM)i/ ^DoNothing [sdb ddb tdb haydb]); # Do not drop here, space instead
  Chain (/NUN|SIN|LAM/ /(JIM)i/ ^DoNothing [sdb ddb tdb haydb]); # Do not drop here, space instead
  Chain (/(NUN|SIN|LAM)[fu]/ /BEi/ ^DoNothing sdb); # Do not drop here, it's fine
  Chain (REf3 /JIMi/ sdb ^DoNothing);

  # Unfortunate double drop...
  Chain /BEi/ ddb.one (REf1 /BEi/ ddb ^DropTwo);

  Chain (/((CH_YE|AIN|JIM)[fu]\d+)|NUN|SIN|LAM|REf3/ /(BE|JIM|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropTwo);
  Chain ([REf1 REf3] /JIMi/ tdb ^DropTwo);

  Chain (REf3 /BEi/ /BEm/ [sdb ddb ddb.one tdb haydb] ^DropOne);
  Chain (REf3 /BEi/ [sdb ddb ddb.one tdb haydb] ^DropTwo /BEm/ [sdb ddb ddb.one tdb haydb] ^DropOne);

  Chain (/(BE)[fu]\d+/ BEi6 [sdb ddb ddb.one tdb haydb] ^DropTwo);
  Chain (/(BE)[fu]\d+/ [sdb ddb ddb.one tdb haydb] BEi6 [sdb ddb ddb.one tdb haydb] ^DropTwo);

  Chain (/(RE|VAO|HAYC|HAYA|DAL|BARI_YE|BE|TE)[fu]\d+/ /(BEi|JIMi|HAYCi(1|2|4|5|6|7|8|9|10|11|12|13|14)$)/ [sdb ddb ddb.one tdb haydb] ^DropOne);
  Chain (/(BE)[fu]\d+/ [sdb ddb ddb.one tdb haydb] /(BEi|JIMi|HAYCi(1|2|4|5|6|7|8|9|10|11|12|13|14)$)/ [sdb ddb ddb.one tdb haydb] ^DropOne);

  Chain (/(KAF|GAF)[fu]\d+/ /(JIMi|HAYCi|BEi)$/ [sdb ddb ddb.one tdb haydb] ^DropOne);

  Chain (@bigbowlfina /(BE|JIM|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropOne);


} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb];

Routine AntiKern400sub {
  Chain (/NUN|SIN|LAM/ [sdb ddb tdb haydb] /(JIM)i/ ^DoNothing [sdb ddb tdb haydb]); # Do not drop here, space instead
  Chain (/NUN|SIN|LAM/ /(JIM)i/ ^DoNothing [sdb ddb tdb haydb]); # Do not drop here, space instead
  Chain (/(NUN|SIN|LAM)[fu]/ /BEi/ ^DoNothing sdb); # Do not drop here, it's fine
  Chain (/REf3/ /BEi/ ^DoNothing [sdb ddb tdb]); # May need height correction...

  # Unfortunate double drop...
  Chain /BEi/ ddb.one (REf1 /BEi/ ddb ^DropTwo);

  # پرچھا (but not رچینی )
  Chain (/REf/ /(JIM)i/ [sdb ddb ddb.one tdb haydb] ^DropOne);
  # پریھا
  Chain (/RE[fu]/ /(BE|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropTwo);

  # پ پہلو
  Chain (/BE[uf]/ /(BE|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropTwo);
  Chain (/BE[uf]/ [sdb ddb tdb] /(BE|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropTwo);

  Chain (/((CH_YE|AIN)[fu]\d+)|NUN|SIN|LAM/ /(BE|JIM|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropTwo /JIM|BE|HAYC/ [sdb ddb tdb haydb] ^DropOne);
  Chain (/((CH_YE|AIN)[fu]\d+)|NUN|SIN|LAM/ /(BE|JIM|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropOne /HAYAm/);
  Chain (/((CH_YE|AIN)[fu]\d+)|NUN|SIN|LAM/ /(BE|JIM|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropTwo);
  Chain (/HAYCu\d+/ /(BEi|JIMi|HAYCi)/ [ddb ddb.one tdb haydb] ^DropTwo);
  Chain (/HAYCf/ /JIMi/ [ddb ddb.one tdb] ^DropTwo);
  Chain (/(HAYC|BARI_YE)[fu]\d+/ /(BEi|JIMi|HAYCi(1|2|4|5|6|7|8|9|10|11|12|13|14)$)/ [ddb ddb.one tdb haydb] ^DropTwo);

  Chain (/(KAF|GAF)[fu]\d+/ JIMi5 [sdb ddb ddb.one tdb haydb] ^DropTwo);
  Chain (/(KAF|GAF)[fu]\d+/ /(JIMi|HAYCi(1|2|4|5|6|7|8|9|10|11|12|13|14)$)/ [sdb ddb ddb.one tdb haydb] ^DropOne);

  Chain (@bigbowlfina /(BE|JIM|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropOne);


} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb];

Routine AntiKern500sub {
  Chain (/NUN|SIN|LAM/ [sdb ddb tdb haydb] /(JIM)i/ ^DoNothing [sdb ddb tdb haydb]); # Do not drop here, space instead
  Chain (/NUN|SIN|LAM/ /(JIM)i/ ^DoNothing [sdb ddb tdb haydb]); # Do not drop here, space instead
  Chain (/(NUN|SIN|LAM)[fu]/ /BEi/ ^DoNothing sdb); # Do not drop here, it's fine
  Chain (/REf3/ /BEi/ ^DoNothing [sdb ddb tdb]); # May need height correction...
  Chain (/REf1/ /BEi/ ^DoNothing [sdb ddb tdb]); # May need height correction...

  # Unfortunate double drop...
  Chain /BEi/ ddb.one (REf1 /BEi/ ddb ^DropTwo);

  Chain (/(RE|VAO|DAL|(CH_YE|AIN)[fu]\d+)|NUN|SIN|LAM/ /(BE|JIM|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropTwo /JIM|BE|HAYC/ [sdb ddb tdb haydb] ^DropOne);

  # فدیم
  Chain (/(RE|VAO|DAL|(CH_YE|AIN)[fu]\d+)|NUN|SIN|LAM/ /(BE|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropTwo);
  #  و چھیا
  Chain (/(RE|VAO|DAL|(CH_YE|AIN)[fu]\d+)|NUN|SIN|LAM/ /(JIM)i/ [ddb ddb.one tdb haydb] ^DropOne);


  Chain (/HAYCu\d+/ /(BEi|JIMi|HAYCi)/ [ddb ddb.one tdb haydb] ^DropTwo);
  Chain (/HAYCf/ /JIMi/ [ddb ddb.one tdb] ^DropTwo);
  Chain (/(HAYC|BARI_YE)[fu]\d+/ /(BEi|JIMi|HAYCi(1|2|4|5|6|7|8|9|10|11|12|13|14)$)/ [ddb ddb.one tdb haydb] ^DropTwo);

  Chain (/(KAF|GAF)[fu]\d+/ JIMi5 [sdb ddb ddb.one tdb haydb] ^DropTwo);
  Chain (/(KAF|GAF)[fu]\d+/ /(JIMi|HAYCi(1|2|4|5|6|7|8|9|10|11|12|13|14)$)/ [sdb ddb ddb.one tdb haydb] ^DropOne);

  Chain (@bigbowlfina /(BE|JIM|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropOne);


} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb];

Routine AntiKern200pos {
  Chain (/[fu]\d+/ /HAYCi(1|2|4|5|6|7|8|9|10|11|12|13|14)$/ ^DoNothingPos) /haydb/ BARI_YEf1;
  Chain (/[fu]\d+/ /HAYCi(1|2|4|5|6|7|8|9|10|11|12|13|14)$/ ^DoNothingPos) BARI_YEf1;

  Chain (/REf/ /(BE|JIM|HAYC)i/ [sdb.two ddb.one ddb.two tdb.two] ^DropATinyBitMore);
  Chain (/.*[fu]\d+/ toeda @inits ^OpenSpaceBeforeKern);
  Chain (/(MIM)[fu]\d+|BEf1|NUN|SIN/ /(JIM|BE|TE)i/ ^OpenSpaceBeforeKern [sdb ddb tdb haydb]);
  Chain (/.*[fu]\d+/ /BEi/ ^OpenSpaceBeforeKern [sdb ddb tdb haydb] /JIMf/);
  Chain (/(RE|VAO|HAYC|DAL|BARI_YE)[fu]\d+/ /(HAYCi3|BEi5)$/ ^OpenSmallSpaceBeforeKern);

  Chain (/(RE|VAO|HAYC|DAL|BARI_YE|MIM)[fu]\d+/ /HAYCi(1|2|4|5|6|7|8|9|10|11|12|13|14)$/ ^OpenSpaceBeforeKern);
  Chain (@bigbowlfina /(BE|JIM|HAYC)i/ ^OpenSmallSpaceBeforeKern /haydb/);
  Chain (/ALIF[fu]/ /(BE|JIM|HAYC)i/ ^OpenSmallSpaceBeforeKern /ddb|tdb/);
  Chain (/[DAL|RE]f/ FEi3 ^OpenSmallSpaceBeforeKern /BE/ /ddb|tdb/);
  Chain (/CH_YE[fu]/ /JIMi/ ^OpenSmallSpaceBeforeKern);
  Chain (@bigbowlfina /(BE|JIM)i/ ^OpenSpaceBeforeKern [sdb.two ddb.two tdb.two] ^DropATinyBitMore);
  Chain (@bigbowlfina /(BE|JIM)i/ ^OpenSpaceBeforeKern); # Needed for e.g. ی ییر
  Chain (REf3 /BEi/ ^OpenSmallSpaceBeforeKern [sdb ddb ddb.one tdb haydb] /BEm/ [sdb ddb ddb.one tdb haydb] );
  Chain (REf3 /BEi/ ^OpenSmallSpaceBeforeKern /BEm/ [sdb ddb ddb.one tdb haydb] );

} IgnoreLigatures UseMarkFilteringSet /sdb|ddb|tdb|haydb|toeda/;

Routine AntiKern400pos {
  Chain (@bariye @inits ^OpenSpaceBeforeKern);
  Chain (/ALIF[fu]/ /(BE|JIM|HAYC)i/ ^OpenSmallSpaceBeforeKern);
  Chain ([@isols @finas] HAYCi9 ^OpenSpaceBeforeKern);
  Chain (@bigbowlfina /(BE|JIM|HAYC)i/ ^OpenSpaceBeforeKern);
  Chain (/HAYCu/ /(BE|JIM|HAYC)i/ ^OpenSpaceBeforeKern);
  # Needed for e.g. وپھی
  Chain (/VAO[uf]|REu/ /(BE|JIM|HAYC)i/ ^OpenSpaceBeforeKern /ddb|tdb|sdb/ ^DropATinyBitMore);
  Chain ([@isols @finas] /BEi|JIMi/ [tdb.one tdb.two sdb.two ddb.two] ^DropATinyBitMore);
} IgnoreLigatures UseMarkFilteringSet /sdb|ddb|tdb|haydb/;

Routine AntiKern500pos {
  Chain (@bariye @inits ^OpenSpaceBeforeKern);
  Chain (/ALIF[fu]/ /(BE|JIM|HAYC)i/ ^OpenSmallSpaceBeforeKern);
  Chain ([@isols @finas] HAYCi9 ^OpenSpaceBeforeKern);
  Chain ([@isols @finas] /JIMi/ ^OpenSpaceBeforeKern [sdb]);
  Chain (@bigbowlfina /(BE|JIM|HAYC)i/ ^OpenSpaceBeforeKern);
  Chain (/HAYCu/ /(BE|JIM|HAYC)i/ ^OpenSpaceBeforeKern);
  Chain ([@isols @finas] /BEi|JIMi/ [tdb.one tdb.two sdb.two ddb.two] ^DropATinyBitMore);

  # سرپھر
  Chain (REf3 /BEi|JIMi/ ^OpenSpaceBeforeKern [tdb]);
  Chain (REf3 /BEi|JIMi/ ^OpenSmallSpaceBeforeKern [tdb ddb]);
} IgnoreLigatures UseMarkFilteringSet /sdb|ddb|tdb|haydb/;

Feature rlig {
  AtHeight 0-99 AntiKern0sub;
  AtHeight 100-199 AntiKern100sub;
  AtHeight 200-399 AntiKern200sub;
  AtHeight 400-499 AntiKern400sub;
  AtHeight 500-600 AntiKern500sub;
  AtHeight 0-399 AntiKern200pos;
  AtHeight 400-499 AntiKern400pos;
  AtHeight 500-600 AntiKern500pos;
  DetectAndSwap top reverse;
  DetectAndSwap bottom;
  DetectAndSwap top;
  Routine OtherFixes {
    Substitute FEi3 (dda) ALIFf1 -> dda.one;
    Substitute /FEi/ sda /FEm/ (dda.one) -> dda.two;
    Substitute /FEi/ dda /FEm/ (sda) -> sda.two;
  };
};

Include "sources/build/fez/pre-mkmk-repositioning.fez";

Feature mkmk {
  Routine DoMarkAttachment {
    Attach &top &_top marks;
    Attach &bottom &_bottom marks;
    Attach &bottom.yb &_bottom.yb marks;
  };
};
