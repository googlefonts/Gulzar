Feature rlig {
  Routine LamAlef {
    Substitute LAMi1 ALIFf1 -> LAM_ALIFu1;
    Substitute LAMm1 ALIFf1 -> LAM_ALIFf1;
  } IgnoreMarks;

  # Override with other final forms

  Routine MoreFinaSelection {
    # These are for REf-group things which go to different final REs in their
    # medial form to in their initial form.
    Substitute FEm1 { REf1 } -> REf2;
    Substitute /^[BT]Em/ { /REf\d+$/ } -> REf3;
    Substitute GAFm1 { REf1 } -> REf4;
  } IgnoreMarks;

  Routine DivineName {
    Substitute ALIFu1 LAMi1 LAMm1 HAYCf1 -> ALLAH;
  } IgnoreMarks;
  # The Glyphs file will contain a set of information about selecting
  # connecting glyphs (what the Qalmi patent calls the Generic Selection
  # Table). Code in the Makefile will extract this information from the
  # Glyphs file and into a CSV file (rules.csv). The custom NastaliqConnections
  # plugin reads this CSV file and creates a set of reverse chaining
  # single substitution rules. It also defines the glyph class
  # @reachable_glyphs, which contains all the glyphs which are actually
  # *used* by the rules. (So if you create a JIMm12 but no connection
  # rule ever substitutes it in, we don't need to care about it.)
  Routine NastaliqConnections {
    LoadPlugin qalamTools.NastaliqConnections;
    NastaliqConnections rules.csv;
  };
};

Feature swsh {
  LoadPlugin qalamTools.SuffixingSubstitute;
  Routine Swash {
    SuffixingSubstitute [BEf1 TEf1 GAFf1 KAFf1 FEf1] -> fs1;
  };
};
